Перем КиоскДляОтбора, АвторДляОтбора, ПродавецДляОтбора, ВидДокументаДляОтбора;
Перем ЗначениеВВидеСтроки, СписокВидовДокументов;
//*************************************
// ПриВыбореКиоска()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореКиоска()   
	
	Если КиоскДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Киоск", КиоскДляОтбора);
		КиоскДляОтбора = КиоскДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореКиоска()
//*************************************
// ПриВыбореПродавца()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореПродавца()   
	
	Если ПродавецДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Продавец", ПродавецДляОтбора);
		ПродавецДляОтбора = ПродавецДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореПродавца()
//*************************************
// ПриВыбореАвтора()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореАвтора()   
	
	Если АвторДляОтбора.Выбран()>0 Тогда
		УстановитьОтбор("Автор", АвторДляОтбора);
		АвторДляОтбора = АвторДляОтбора.ТекущийЭлемент();
	Иначе
		УстановитьОтбор("");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореАвтора()
//******************************************************************************
// ПриВыбореВидаДокумента()
//
// Параметры:
//  Нет
//
// Описание:
//  Отрабатывает изменение значения в "быстром" отборе 
//
Процедура ПриВыбореВидаДокумента()
	
	Если ПустоеЗначение(ВидДокументаДляОтбора) = 1 Тогда
		ВидДокументаДляОтбора=1;
	КонецЕсли;
	УстановитьОтбор(СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора), );
	
КонецПроцедуры // ПриВыбореВидаДокумента() 
//*************************************
// ПриУстановкеБыстрогоОтбора()
//
// Параметры:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Поле со списком "Вид отбора"
//
// Описание:
//  Устанавливает отбор в журнале по выбранному виду 
Процедура ПриУстановкеБыстрогоОтбора()
	Перем ТекущДок;                           
	
	ТекущДок = ТекущийДокумент;
	ОтборПо  = ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока());
	
	Если ВидОтбора.ТекущаяСтрока() = 1 Тогда	// нет быстрого отбора
		    
		ЗначениеВВидеСтроки = "";
		Форма.кнЗначение.Доступность(0);
		
		УстановитьОтбор("");
		ВидыОтбора("*");

	Иначе	// есть быстрый отбор
        
		Форма.кнЗначение.Доступность(1);
		
		Если ОтборПо = "по киоску" Тогда   
			ПриВыбореКиоска();
			ЗначениеВВидеСтроки=Строка(КиоскДляОтбора);
			
		ИначеЕсли ОтборПо = "по продавцу" Тогда
			ПриВыбореПродавца();                   
			ЗначениеВВидеСтроки = Строка(ПродавецДляОтбора);
			
		ИначеЕсли ОтборПо = "по автору" Тогда
			ПриВыбореАвтора();                          
			ЗначениеВВидеСтроки=Строка(АвторДляОтбора);
        ИначеЕсли ОтборПо = "по виду документов" Тогда
			ПриВыбореВидаДокумента();                   
			СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора,ЗначениеВВидеСтроки);
			
		КонецЕсли;
		
		ВидыОтбора("");
		
	КонецЕсли;
	
	Попытка
		АктивизироватьОбъект(ТекущДок);
	Исключение	
		Сообщить(ОписаниеОшибки(),"!");
	КонецПопытки;	
		
КонецПроцедуры // ПриУстановкеБыстрогоОтбора()
//*************************************  
// ПоКнопкеЗначение()
//
// Параметры:
//  Нет
//
// Вызывается из формул элементов диалога:
//  По кнопке "Значение..."
//
// Описание:
//  Обрабатывает выбор значения отбора в журнале 
//
Процедура ПоКнопкеЗначение()  
	Перем ЗначениеИзменилось,СтароеЗначение,ВыбЗначение;                     
	
	ЗначениеИзменилось=0;     
	ОтборПо           = ВидОтбора.ПолучитьЗначение(ВидОтбора.ТекущаяСтрока());

	
	Если ОтборПо = "по киоску" Тогда
		СтароеЗначение=КиоскДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Киоски");
		Если ВыбЗначение.Выбрать("Выберите киоск","ФормаСписка")=1 Тогда
			КиоскДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;	
		
	ИначеЕсли ОтборПо = "по автору" Тогда
		СтароеЗначение=АвторДляОтбора;
		ВыбЗначение = СоздатьОбъект("Справочник.Пользователи");
		Если ВыбЗначение.Выбрать("Выбор автора","ДляВыбора")=1 Тогда
			АвторДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли;
	ИначеЕсли ОтборПо = "по продавцу" Тогда
		СтароеЗначение	=	ПродавецДляОтбора;
		ВыбЗначение 	= СоздатьОбъект("Справочник.Продавцы");
		Если ВыбЗначение.Выбрать("Выбор продавца","ФормаСписка")=1 Тогда
			ПродавецДляОтбора = ВыбЗначение;
			Если ВыбЗначение<>СтароеЗначение Тогда
				ЗначениеИзменилось=1;    
			КонецЕсли;	
		КонецЕсли; 
		
	ИначеЕсли ОтборПо = "по виду документов" Тогда
		СтароеЗначение=ВидДокументаДляОтбора;
		Если ПустоеЗначение(ВидДокументаДляОтбора)>0 Тогда
			ВыбЗначение = "";
		Иначе	
			ВыбЗначение = СписокВидовДокументов.ПолучитьЗначение(ВидДокументаДляОтбора);
		КонецЕсли;	
		СписокВидовДокументов.ВыбратьЗначение(ВыбЗначение,"Выбор вида документов",,60,0);
		ВидДокументаДляОтбора = СписокВидовДокументов.НайтиЗначение(ВыбЗначение);
		Если ВидДокументаДляОтбора<>СтароеЗначение Тогда 
			ЗначениеИзменилось=1;    
		КонецЕсли;	 
	

	КонецЕсли;	
	        
	Если ЗначениеИзменилось=1 Тогда
		ПриУстановкеБыстрогоОтбора();
	КонецЕсли;	

КонецПроцедуры // ПоКнопкеЗначение()  
//*************************************
Процедура ПриОткрытии()
	
КонецПроцедуры
//**************************************************************
СписокВидовДокументов = СоздатьОбъект("СписокЗначений");
Для Счетчик = 1 по Метаданные.Документ() Цикл
	СписокВидовДокументов.ДобавитьЗначение(Метаданные.Документ(Счетчик).Идентификатор, Метаданные.Документ(Счетчик).Представление());
КонецЦикла;
СписокВидовДокументов.СортироватьПоПредставлению();

// Заполним список возможных видов быстрого отбора
ВидОтбора.ДобавитьЗначение(" < отсутствует > ");
ВидОтбора.ДобавитьЗначение("по виду документов");
ВидОтбора.ДобавитьЗначение("по киоску"); 
ВидОтбора.ДобавитьЗначение("по продавцу");
ВидОтбора.ДобавитьЗначение("по автору"); 

КиоскДляОтбора	= СоздатьОбъект("Справочник.Киоски");  
АвторДляОтбора	= СоздатьОбъект("Справочник.Пользователи");
ПродавецДляОтбора	= СоздатьОбъект("Справочник.Продавцы");

ЗначениеВВидеСтроки = "";