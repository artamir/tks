Перем хмлФильтр, тзФильтр;
//*******************************************
Процедура ВывестиПараметры(сзПредопр)
	
	ТабличноеПоле.Очистить();
	ТабличноеПоле.НоваяКолонка("П",,,,, 2);
	ТабличноеПоле.НоваяКолонка("Параметр",,,,, 10); 
	ТабличноеПоле.НоваяКолонка("Значение",,,,, 10); 
	
	тзФильтр.ВыбратьСтроки();
	Пока тзФильтр.ПолучитьСтроку() = 1 Цикл
		
		ТабличноеПоле.НоваяСтрока();
		ТабличноеПоле.Параметр = тзФильтр.Параметр;
		Если ТипЗначения(сзПредопр) = 100 Тогда
			ТабличноеПоле.Значение = сзПредопр
				.Получить(ТабличноеПоле.Параметр);
			ТабличноеПоле.П = ?(ПустоеЗначение(ТабличноеПоле
				.Значение) = 1, "", "Х");
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	
//*******************************************
Процедура ПриОткрытии()
	
	Если глмХМЛ.СоздатьЭлементДокумента(хмлФильтр, "", "", 
		"Фильтр", "windows-1251") = 0 Тогда
			Сообщить("Не удалось создать xml-документ", "!");
			СтатусВозврата(0);
			Возврат;
	КонецЕсли;	

	Параметр = Форма.Параметр;

	Если ТипЗначения(Параметр) = 100 Тогда
		
		глмФильтр.ПрочитатьПараметры(Параметр
			.Получить("хмлСхема").childNodes(1), 
				хмлФильтр.childNodes(1), 
					тзФильтр, "1");
        
		ВывестиПараметры(Параметр.Получить("сзПредопр"));

		Если Параметр.Получить("БезОткрытияОкнаВыбора") = 1 Тогда
			
			глмФильтр.ЗаписатьПараметры(хмлФильтр, 
				тзФильтр, ТабличноеПоле);
			
			Форма.Параметр.ДобавитьЗначение(хмлФильтр, "Результат");
			
			СтатусВозврата(0);
			Возврат;
			
		КонецЕсли;

	КонецЕсли;	
	
КонецПроцедуры	
//*******************************************
Процедура ТабличноеПолеПриВыбореСтроки()
	Перем ВыбТип, ВыбЗнач;
	
	ТекСтр = ТабличноеПоле.ТекущаяСтрока();
	
	Если ТабличноеПоле.ПолучитьЗначение(ТекСтр, 
		"П") = "Х" Тогда
			Возврат;
	КонецЕсли;	
	
	Тип = тзФильтр.ПолучитьЗначение(ТекСтр, "Тип");
	
	Если Тип = "ПолеСоСписком" Тогда
		Список = тзФильтр.ПолучитьЗначение(ТекСтр, "Варианты");
		Если Список.ВыбратьЗначение(ВыбЗнач,,,, 1) = 1 Тогда
			
			ТабличноеПоле.УстановитьЗначение(ТекСтр, 
				"Значение", ВыбЗнач);
			
		КонецЕсли;
	ИначеЕсли Тип = "Неопределенный" Тогда
		тзОбъекты = глмМетаданные.ВыбратьОбъекты(глмМетаданные
			.ПолучитьПустойИд());
		
		сзВидыОб = СоздатьОбъект("СписокЗначений");
		сзВидыОб.ДобавитьЗначение("Строка");
		сзВидыОб.ДобавитьЗначение("Число"); 
		сзВидыОб.ДобавитьЗначение("Дата");
		
		тзОбъекты.ВыбратьСтроки();
		Пока тзОбъекты.ПолучитьСтроку() = 1 Цикл
			Если глмМетаданные.Ид2Список(тзОбъекты.Ид)
				.ПолучитьЗначение(1) = "U" Тогда
					Продолжить;
				
			ИначеЕсли глмМетаданные.ЭтоКаталог(тзОбъекты.Ид) = 0 Тогда
				Об = глмМетаданные.Ид2Ссылка(тзОбъекты.Ид);
				сзВидыОб.ДобавитьЗначение(ТипЗначенияСтр(Об) 
					+ "." + Об.Вид(), Об.Вид());
			КонецЕсли;	
		КонецЦикла;	
		
		Если сзВидыОб.ВыбратьЗначение(ВыбТип, "Выбор типа объекта", , 60) = 1 Тогда
			Если ВвестиЗначение(ВыбЗнач,, ВыбТип) = 1 Тогда
				
				ТабличноеПоле.УстановитьЗначение(ТекСтр, 
					"Значение", ВыбЗнач);
				
			КонецЕсли;		
		КонецЕсли;			
	Иначе
		Если ВвестиЗначение(ВыбЗнач,, Тип) = 1 Тогда
			
			ТабличноеПоле.УстановитьЗначение(ТекСтр, 
				"Значение", ВыбЗнач);
			
		КонецЕсли;
	КонецЕсли;		
	
КонецПроцедуры	
//*******************************************
Процедура Выполнить(НеМенятьФильтр=0)
    
	глмФильтр.ЗаписатьПараметры(хмлФильтр, 
		тзФильтр, ТабличноеПоле, НеМенятьФильтр);

	//Форма.Параметр = хмлФильтр; 
	Форма.Параметр.ДобавитьЗначение(хмлФильтр, "Результат");
	
КонецПроцедуры
//*******************************************
тзФильтр = СоздатьОбъект("ТаблицаЗначений");
тзФильтр.НоваяКолонка("Параметр");		 
тзФильтр.НоваяКолонка("Путь");
тзФильтр.НоваяКолонка("Тип");
тзФильтр.НоваяКолонка("Варианты");


		