Перем Скрипт, Модуль Экспорт;
//*******************************************
Функция Тест()
	
	Возврат """глмБлокировка"" - подключен";
	
КонецФункции
//*******************************************
Функция ПолноеИмяФайла(ИмяФайла = "1sjourn.dbf") Экспорт
	
	Возврат СтрЗаменить(СтрЗаменить(КаталогИБ() 
		+ ИмяФайла, "\", "\\"), "\\\\", "\\");
	
КонецФункции	
//*******************************************
Функция ОткрФайл(ИмяФайла) Экспорт
	
	Возврат Модуль.CreateFile(ИмяФайла, 
	Модуль.GENERIC_READ_WRITE, 
		Модуль.FILE_SHARE_READ_WRITE, 0, 
			Модуль.OPEN_EXISTING, 
				Модуль.FILE_ATTRIBUTE_NORMAL, 0);
	
КонецФункции	
//*******************************************
Функция БлокТаблНаЗапись(ИмяТаблицы) Экспорт
	
	ИмяФайла = ПолноеИмяФайла(ИмяТаблицы);
	
	ФайлТаблицы = ОткрФайл(ИмяФайла);
	
	Если ФайлТаблицы <> -1 Тогда
		Рез = Модуль.LockFile(ФайлТаблицы, 
			1073741823, 0, 1, 0);
		Если Рез <> - 1 Тогда
			Рез = Модуль.LockFile(ФайлТаблицы, 
				1073741824, 0, 1073741823, 0);
		КонецЕсли;
	Иначе
		Рез = -1;
	КонецЕсли;

	Возврат ?(Рез = -1, Рез, ФайлТаблицы);	
	
КонецФункции	
//*******************************************
// Внимание! Если блок на чтение таблицы не будет 
// снят некоторое (непродолжительное) время, клиенты
// 1с будут "валиться" с сообщением об ошибке ИБ
// 
Функция БлокТаблНаЧтение(ФайлТаблицы) Экспорт
	
	Если ФайлТаблицы <> -1 Тогда
		Рез = Модуль.LockFile(ФайлТаблицы, 
			1073741772, 0, 1, 0);
		Если Рез <> - 1 Тогда
			Рез = Модуль.LockFile(ФайлТаблицы, 
				1073741773, 0, 50, 0);
		КонецЕсли;
	Иначе
		Рез = -1;
	КонецЕсли;

	Возврат ?(Рез = -1, Рез, ФайлТаблицы);	
		
КонецФункции
//*******************************************
Функция СнятьБлокНаЗапись(ФайлТаблицы) Экспорт
	
	Если ФайлТаблицы <> -1 Тогда
		Рез = Модуль.UnlockFile(ФайлТаблицы, 
			1073741823, 0, 1, 0);
		Если Рез <> - 1 Тогда
			Рез = Модуль.UnlockFile(ФайлТаблицы, 
				1073741824, 0, 1073741823, 0);
		КонецЕсли;
	Иначе
		Рез = -1;
	КонецЕсли;
    
	Возврат ?(Рез = -1, Рез, 
		Модуль.CloseHandle(ФайлТаблицы));	
	
КонецФункции
//*******************************************
Функция СнятьБлокНаЧтение(ФайлТаблицы) Экспорт
	
	Если ФайлТаблицы <> -1 Тогда
		Рез = Модуль.UnlockFile(ФайлТаблицы, 
			1073741772, 0, 1, 0);
		Если Рез <> - 1 Тогда
			Рез = Модуль.UnlockFile(ФайлТаблицы, 
				1073741773, 0, 50, 0);
		КонецЕсли;
	Иначе
		Рез = -1;
	КонецЕсли;

	Возврат ?(Рез = -1, Рез, ФайлТаблицы);	
	
КонецФункции
//*******************************************
Функция БлокИндекса(ИмяИндекса) Экспорт
	
	ИмяФайла = ПолноеИмяФайла(ИмяИндекса);
	
	ФайлИндекса = ОткрФайл(ИмяФайла);
	
	Если ФайлИндекса <> -1 Тогда
		Рез = Модуль.LockFile(ФайлИндекса, 
			2147483646, 0, 1, 0);
	Иначе
		Рез = -1;
	КонецЕсли;

	Возврат ?(Рез = -1, Рез, ФайлИндекса);	
	
КонецФункции
//*******************************************
Функция СнятьБлокИндекса(ФайлИндекса) Экспорт
	
	Если ФайлИндекса <> -1 Тогда
		Рез = Модуль.UnlockFile(ФайлИндекса, 
			2147483646, 0, 1, 0);
	Иначе
		Рез = -1;
	КонецЕсли;
    
	Возврат ?(Рез = -1, Рез, 
		Модуль.CloseHandle(ФайлИндекса));	
	
КонецФункции
//*******************************************
Скрипт = СоздатьОбъект("MSScriptControl.ScriptControl");
Скрипт.Language = "JScript"; 
Скрипт.AddCode("
	|GENERIC_READ = 0x80000000;
	|GENERIC_WRITE = 0x40000000;
	|GENERIC_READ_WRITE = GENERIC_READ|GENERIC_WRITE;
	|FILE_SHARE_READ = 1;
	|FILE_SHARE_WRITE = 2;
	|FILE_SHARE_READ_WRITE = FILE_SHARE_READ|FILE_SHARE_WRITE;
	|OPEN_EXISTING = 3;
	|OPEN_ALWAYS = 4;
	|FILE_ATTRIBUTE_NORMAL = 128;
	|ONES_BLOCK_AREA = 0x3FFFFFFF;
	|
	|WinAPI = new ActiveXObject(""DynamicWrapperX.2"");
	|WinAPI.Register(""kernel32"", ""CreateFile"", ""i=sllllll"", ""r=l"");
	|WinAPI.Register(""kernel32"", ""LockFile"", ""i=hllll"", ""r=l"");
	|WinAPI.Register(""kernel32"", ""UnlockFile"", ""i=hllll"", ""r=l"");
	|WinAPI.Register(""kernel32"", ""CloseHandle"", ""i=h"", ""r=l"");
    |
	|function CreateFile(
	|	lpFileName,             // имя файла
	|	dwDesiredAccess,        // режим доступа
	|	dwShareMode,            // совместный доступ
	|	lpSecurityAttributes, 	// SD (дескр. защиты)
	|	dwCreationDisposition,  // как действовать
	|	dwFlagsAndAttributes,   // атрибуты файла
	|	hTemplateFile           // дескр.шаблона файла
	|	) { return WinAPI.CreateFile(lpFileName, 
	|		dwDesiredAccess, dwShareMode, lpSecurityAttributes, 
	|			dwCreationDisposition, dwFlagsAndAttributes, hTemplateFile) }
	|
	|function LockFile(
	|	hFile,                    // дескриптор файла
	|	dwFileOffsetLow,          // младшее слово смещения
	|	dwFileOffsetHigh,         // старшее слово смещения
	|	nNumberOfBytesToLockLow,  // младшее слово длины
	|	nNumberOfBytesToLockHigh  // старшее слово длины
	|	){ return WinAPI.LockFile(hFile, dwFileOffsetLow, dwFileOffsetHigh, 
	|		nNumberOfBytesToLockLow, nNumberOfBytesToLockHigh) }
	|
	|function UnlockFile(
	|	hFile,                     // дескриптор файла
	|	dwFileOffsetLow,           // младшее слово начала
	|	dwFileOffsetHigh,          // старшее слово начала
	|	nNumberOfBytesToUnlockLow, // младшее слово длины
	|	nNumberOfBytesToUnlockHigh // старшее слово длины
    |	){ return WinAPI.UnlockFile(hFile, dwFileOffsetLow, dwFileOffsetHigh, 
	|		nNumberOfBytesToUnlockLow, nNumberOfBytesToUnlockHigh) }
	|
	|function CloseHandle(
	|	hObject   // дескриптор объекта
	|	) {return WinAPI.CloseHandle(hObject) }
	|");
	
Модуль = Скрипт.Modules("Global").CodeObject;
	
