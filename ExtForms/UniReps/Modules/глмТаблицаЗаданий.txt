Перем мРазмерСтатистики;
//*******************************************
Функция Тест() Экспорт

	Возврат "Глобальный модуль ""ТаблицаЗаданий"" успешно подключен";

КонецФункции
//*******************************************
Функция ИнТаблСтат(				// создание таблицы
	// статистики выполнения заданий в фоновом режиме
	)
	
	Табл = СоздатьОбъект("ТаблицаЗначений");
	Табл.НоваяКолонка("Дата"); 
	Табл.НоваяКолонка("Время"); 
	Табл.НоваяКолонка("Задание");
	Табл.НоваяКолонка("Результат");
    
	Возврат Табл;
	
КонецФункции
//*******************************************
Функция ИнТаблЗаданий(			// создание таблицы
	// заданий в фоновом режиме
	)
	
	Табл = СоздатьОбъект("ТаблицаЗначений");
	Табл.НоваяКолонка("Задание"); 
	Табл.НоваяКолонка("Параметры"); 
	Табл.НоваяКолонка("Условие");
	Табл.НоваяКолонка("Однократно"); 
	Табл.НоваяКолонка("Задержек");  
	Табл.НоваяКолонка("Отказов");  
	Табл.НоваяКолонка("Период");
	Табл.НоваяКолонка("Таймер"); 
	Табл.НоваяКолонка("Ссылка");
	
	Возврат Табл;

КонецФункции
//*************************************
Функция ПланЗадания(   			// поставить задание в 
	//  план на выполнение
	тзЗаданий,                  // таблица заданий
	стрЗадание,					// текст задачи, инструкции
	чОднократно,                // однократное выполнение
	чПериод,                    // период выполнения
	чЗадержек,                  // число пропускаемых тактов 
	Ссылка	                    // ссылка на элемент справочника
	) Экспорт
    
	спЗадание = СоздатьОбъект("СписокЗначений");
	
	Попытка
		// попытка чтения задания вместе с условием 
		// и списком параметров
		спЗадание.ИзСтрокиСРазделителями(стрЗадание);

		Если ТипЗначения(спЗадание) <> 100 Тогда
			// неудача
		    Сообщить("Не удалось прочитать параметры "
				+ "задания """ + стрЗадание + """", "!");
			Возврат 0;
			
		КонецЕсли;
		
	Исключение
		// неудача
		Сообщить(ОписаниеОшибки());
		Возврат 0;
		
	КонецПопытки;	
	
	тзЗаданий.НоваяСтрока();
	
	тзЗаданий.Задание = спЗадание.ПолучитьЗначение(1);  
	тзЗаданий.Условие = спЗадание.ПолучитьЗначение(2);
	
	тзЗаданий.Параметры = СоздатьОбъект("СписокЗначений");
	
	Если спЗадание.РазмерСписка() > 2 Тогда

		спЗадание.Выгрузить(тзЗаданий.Параметры, 3, 
			спЗадание.РазмерСписка() - 2);

	КонецЕсли;
		
	тзЗаданий.Период 		= чПериод; 
	тзЗаданий.Таймер 		= 1;
	тзЗаданий.Однократно 	= чОднократно;  
	тзЗаданий.Задержек 		= чЗадержек; 
	тзЗаданий.Отказов 		= 0;
	тзЗаданий.Ссылка 		= Ссылка;
	
	Возврат 1;
	
КонецФункции
//*******************************************
Процедура УдалитьЗадание(тзЗаданий, стрЗадание)
	
	НС = 0;
	Если тзЗаданий.НайтиЗначение(стрЗадание, 
		НС, "Задание") = 1 Тогда
			
			тзЗаданий.УдалитьСтроку(НС);
			
	КонецЕсли;
		
КонецПроцедуры				
//*******************************************
мРазмерСтатистики = 1000;
