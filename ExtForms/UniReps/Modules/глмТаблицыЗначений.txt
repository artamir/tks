//*******************************************
Функция Тест()
	
	Возврат """Таблицы значений"" - подключен";
	
КонецФункции

//*******************************************
Функция ВыбратьПоЗначению(				// выбирает строки таблицы по значению в колонке
										// параметры:
	Знач ТЗ, 							//  исходная таблица	
	Колонка, 							//  номер или наименование колонки
	Значение)							//  искомое значение
	
	Перем БТ;

	ТЗ.Сортировать(Колонка); 
	
	СтартСтрока = 0;

	Если ТЗ.НайтиЗначение(Значение, СтартСтрока, Колонка) = 1 Тогда

		ФинишСтрока = СтартСтрока + 1;

		Пока ФинишСтрока <= ТЗ.КоличествоСтрок() Цикл

			Если ТЗ.ПолучитьЗначение(ФинишСтрока, Колонка) <> Значение Тогда
				Прервать;
			КонецЕсли;

			ФинишСтрока = ФинишСтрока + 1;

		КонецЦикла;

		ТЗ.Выгрузить(БТ, СтартСтрока, ФинишСтрока - 1);

	КонецЕсли;	
	
	Возврат БТ;
	
КонецФункции

//*******************************************
Функция ЗаменаКолонки(					// замена имени колонки в соответствии со списком замены
	                                    // параметры:
	ИмяКол,                             //	наименование колонки
	сзКолЗамены = "",                   //	список замены
	Напр = 0)                           //	направление: 0 - исходное на заменяемое, 1 - заменяемое на исходное
	
	Если сзКолЗамены = "" Тогда
		Возврат ИмяКол;
	КонецЕсли;	
	
	ИмяЗамены = ?(Напр = 0, сзКолЗамены.Получить(ИмяКол), 
		глмОбщиеФункции.ПолучПредстЗначСписка(сзКолЗамены, 
			"", ИмяКол));
	
	Возврат ?(ПустоеЗначение(ИмяЗамены) = 0, 
		ИмяЗамены, ИмяКол);		
	
КонецФункции

//*******************************************
Функция СоздатьЗапись(					// формирует отдельную однострочную таблицу 
										// из значений текущей строки исходной таблицы
										// параметры:
	Таб,                    			//	исходная таблица
	сзКолВкл = "",          			//	список колонок включения в результирующий набор
	сзКолИскл = "",         			//	--//--	исключения --//--
	сзКолЗамены = "")					//	замены наименования исходных колонок в результирующей таблице
	
	Рез = СоздатьОбъект("ТаблицаЗначений");
	
	Рез.НоваяСтрока();
	
	Для НомКол = 1 По Таб.КоличествоКолонок() Цикл
		
		ИмяКол = Таб.ПолучитьПараметрыКолонки(НомКол);

		Если сзКолВкл <> "" Тогда
			Если сзКолВкл.Принадлежит(ИмяКол) = 0 Тогда
				Продолжить;
				
			КонецЕсли;

		ИначеЕсли сзКолИскл <> "" Тогда
			Если сзКолИскл.Принадлежит(ИмяКол) = 1 Тогда
				Продолжить;
				
			КонецЕсли;	
		КонецЕсли;	

		ИмяЗамены = ЗаменаКолонки(ИмяКол, сзКолЗамены, 0);
		
		Рез.НоваяКолонка(ИмяЗамены);
        
		Если Таб.НомерСтроки > 0 Тогда
			
			Рез.УстановитьЗначение(1, ИмяЗамены, 
				Таб.ПолучитьЗначение(Таб.НомерСтроки, ИмяКол));
				
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции	

//*******************************************
Функция ОбнулитьЗапись(					// формирует запись пустых значений на основе переданной записи
										// параметры:
	Запись)								//	исходная запись
	Перем Рез;
	
	Запись.Выгрузить(Рез, 1, 1);
	Рез.Заполнить("");
	
	Возврат Рез;
	
КонецФункции	

//*******************************************
Функция ПрочитатьЗапись(				// формирует отдельную однострочную таблицу 
										// из значений текущей строки исходной таблицы
										// параметры:
	Таб,                                //	исходная таблица
	Запись,								//	результирующая запись
	сзКолЗамены = "")					//	замены наименования исходных колонок в результирующей таблице
	
	Запись = ОбнулитьЗапись(Запись);
	
	Для НомКол = 1 По Запись.КоличествоКолонок() Цикл
		
		ИмяЗамены = Запись.ПолучитьПараметрыКолонки(НомКол);
		
		ИмяКол = ЗаменаКолонки(ИмяЗамены, сзКолЗамены, 1);
		
		Запись.УстановитьЗначение(1, ИмяЗамены, 
			Таб.ПолучитьЗначение(Таб.НомерСтроки, ИмяКол));
		
	КонецЦикла;	
	
	Возврат Запись;
	
КонецФункции

//*******************************************
Функция ЗаписатьЗапись(					// записывает в текущей строке исходной таблицы
										// значения из отдельной однострочной таблицы 
										// параметры:
	Таб,                                //	результирующая таблица
	Запись,								//	исходная запись
	сзКолЗамены = "")					//	замены наименования исходных колонок в результирующей таблице
	
	Для НомКол = 1 По Запись.КоличествоКолонок() Цикл
		
		ИмяЗамены = Запись.ПолучитьПараметрыКолонки(НомКол);
		
		ИмяКол = ЗаменаКолонки(ИмяЗамены, сзКолЗамены, 1);
		
		Таб.УстановитьЗначение(Таб.НомерСтроки, ИмяКол, 
			Запись.ПолучитьЗначение(1, ИмяЗамены));
		
	КонецЦикла;	
	
	Возврат Запись;
	
КонецФункции

//*******************************************
Функция ЭтоНулеваяЗапись(				// проверяет наличие данных в переданной записи
										// параметры:
	Запись)								//	запись	
	
	Для НомКол = 1 По Запись
		.КоличествоКолонок() Цикл
			Если ПустоеЗначение(Запись
				.ПолучитьЗначение(1, НомКол)) = 0 Тогда
					Возврат 0;
			КонецЕсли;
	КонецЦикла;
	
	Возврат 1;
	
КонецФункции

//*******************************************
Процедура ПерестроитьТаблицуПоКлючу(	// сортирует таблицу по заданным колонкам
										// параметры:
	Таб, 								//	исходная таблица
	Ключ)								//	колонки, заданные записью
	
	Если Таб.КоличествоСтрок() > 0 Тогда
		Колонки = "";
		Для НомКол = 1 По Ключ.КоличествоКолонок() Цикл
			Колонки = Колонки + ?(Колонки = "", "*", ",*")
				+ Ключ.ПолучитьПараметрыКолонки(НомКол);
		КонецЦикла;		
		Таб.Сортировать(Колонки);
		Таб.ПолучитьСтрокуПоНомеру(1);
	КонецЕсли;
	
КонецПроцедуры

//*******************************************
Функция ПолучитьЗначениеТаблицыПоКлючу(	// получает из исходной таблицы запись по
										// заданным ключевым значениям	
										// параметры:
	Таб, 								//	исходная таблица
	Ключ,                               //	ключевые значения
	Запись,								//	колонки, заданные записью
	ПустЗнач = 0)						//	возможность подбора записи, с пустыми 
										//	значениями в колонках, начиная с 
										//	некоторой позиции
	Если Таб.КоличествоСтрок() > 0 Тогда
		
		НомСтр = Таб.НомерСтроки;
		
		Пока НомСтр <= Таб.КоличествоСтрок() Цикл
			
			НомКол = 1;
			
			Пока НомКол <= Ключ.КоличествоКолонок() Цикл
				
				КлючЗначение = Ключ.ПолучитьЗначение(1, НомКол);
				
				ТабЗначение = Таб.ПолучитьЗначение(НомСтр, 
					Ключ.ПолучитьПараметрыКолонки(НомКол));
				
				Если КлючЗначение = ТабЗначение Тогда
	// все нормально, двигаемся по выбранной записи
						
				ИначеЕсли (ПустЗнач = 1) И (НомКол > 1)
					И (ПустоеЗначение(ТабЗначение) = 1) Тогда		
	// все нормально, двигаемся по выбранной записи,
	// но, только после подбора первого поля ключа
					
				ИначеЕсли глмОбщиеФункции
					.СравнитьЗначения(КлючЗначение, 
						ТабЗначение, ">") = 1 Тогда
	// запись нужно пропустить и перейти к следующей
							Прервать;
				Иначе
	// превысили значение сортировки (не нашли...)
						Таб.ПолучитьСтрокуПоНомеру(НомСтр);
						Возврат ОбнулитьЗапись(Запись);
				КонецЕсли;
				
				НомКол = НомКол + 1;
			КонецЦикла;
			
			Если НомКол > Ключ.КоличествоКолонок() Тогда
	// нашли запись
				Прервать;
			КонецЕсли;	
			
			НомСтр = НомСтр + 1;
		КонецЦикла;
		
		Если НомСтр > Таб.КоличествоСтрок() Тогда

			Таб.ПолучитьСтрокуПоНомеру(Таб
				.КоличествоСтрок());

			Возврат ОбнулитьЗапись(Запись);
		Иначе

			Таб.ПолучитьСтрокуПоНомеру(НомСтр);

			Возврат ПрочитатьЗапись(Таб, Запись, "");
		КонецЕсли;	
	Иначе
	// таблица не содержит записей	
		Возврат ОбнулитьЗапись(Запись);
	КонецЕсли;

КонецФункции

//*******************************************
Функция ЛевоеСоединение(                // соединение таблиц, результирующая таблица
										// содержит все строки "левой" таблицы, дополненные
										// значениями "правой" таблицы по строкам совпадающим по ключу
										// параметры:
	Знач ТабЛевая, 						//	левая (исходная) таблиа
	Знач ТабПравая,                     //	правая (присоединяемая) таблица
	Ключ,                               //	ключевой набор полей, заданный в виде записи
	сзКолЗамены = "",                   //	список замены
	ПустЗнач = 1)                       //	возможность подбора записи, с пустыми 
										//	значениями в колонках, начиная с 
										//	некоторой позиции
										
	ПерестроитьТаблицуПоКлючу(ТабПравая, Ключ);
	
	Запись = СоздатьЗапись(ТабПравая, "", "", сзКолЗамены);
	
	Для НомКол = 1 По Запись.КоличествоКолонок() Цикл

		ИмяЗамены = Запись.ПолучитьПараметрыКолонки(НомКол);
		
		ИмяКол = ЗаменаКолонки(ИмяЗамены, сзКолЗамены, 1);
		
		Попытка
			ТабЛевая.ПолучитьПараметрыКолонки(ИмяКол);
			
		Исключение
			ТабЛевая.НоваяКолонка(ИмяКол);
			
		КонецПопытки;
		
	КонецЦикла;	
	
	ПерестроитьТаблицуПоКлючу(ТабЛевая, Ключ);
	
	Для НомСтр = 1 По ТабЛевая.КоличествоСтрок() Цикл
		
		ТабЛевая.ПолучитьСтрокуПоНомеру(НомСтр);
		
		Ключ = ПрочитатьЗапись(ТабЛевая, Ключ, "");
		
		Запись = ПолучитьЗначениеТаблицыПоКлючу(ТабПравая, Ключ, Запись, ПустЗнач);
	    
		Если ЭтоНулеваяЗапись(Запись) = 0 Тогда
			
			ЗаписатьЗапись(ТабЛевая, Запись, сзКолЗамены);
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат ТабЛевая;	
	
КонецФункции
